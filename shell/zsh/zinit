#!/usr/bin/zsh

# init
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# theme
zinit ice pick"async.zsh" src"pure.zsh" # with zsh-async library that's bundled with it.
zinit light sindresorhus/pure

# zsh-defer
zinit light romkatv/zsh-defer
source "$HOME/.local/share/zinit/plugins/romkatv---zsh-defer/zsh-defer.plugin.zsh"

# zsh-autosuggestions
zsh-defer zinit light zsh-users/zsh-autosuggestions

# fast-syntax-highlighting
zsh-defer zinit light zdharma-continuum/fast-syntax-highlighting

# zsh-vi-mode
zvm_after_init_commands=("source <(fzf --zsh)")
function zvm_config() {
  ZVM_VI_SURROUND_BINDKEY=s-prefix
  ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
}
zinit ice depth=1
zinit light jeffreytse/zsh-vi-mode

# fzf-tab
zsh-defer zinit light Aloxaf/fzf-tab


#######################
# zsh-vi-mode overrides
#######################

# Read clipboard (cbread) and write clipboard (cbcopy)
cbread() {
  if [[ -n $WAYLAND_DISPLAY ]]; then
    wl-paste
  elif [[ -n $DISPLAY ]]; then
    xsel --clipboard --output
  fi
}

cbprint() {
  if [[ -n $WAYLAND_DISPLAY ]]; then
    wl-copy
  elif [[ -n $DISPLAY ]]; then
    xsel --clipboard --input
  fi
}

my_zvm_vi_yank() {
    zvm_vi_yank
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_delete() {
    zvm_vi_delete
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_change() {
    zvm_vi_change
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_change_eol() {
    zvm_vi_change_eol
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_substitute() {
    zvm_vi_substitute
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_substitute_whole_line() {
    zvm_vi_substitute_whole_line
    echo -en "${CUTBUFFER}" | cbprint
}

my_zvm_vi_put_after() {
    CUTBUFFER=$(cbread)
    zvm_vi_put_after
    zvm_highlight clear # zvm_vi_put_after introduces weird highlighting
}

my_zvm_vi_put_before() {
    CUTBUFFER=$(cbread)
    zvm_vi_put_before
    zvm_highlight clear # zvm_vi_put_before introduces weird highlighting
}

my_zvm_vi_replace_selection() {
    CUTBUFFER=$(cbread)
    zvm_vi_replace_selection
    echo -en "${CUTBUFFER}" | cbprint
}


zvm_after_lazy_keybindings() {
    zvm_define_widget my_zvm_vi_yank
    zvm_define_widget my_zvm_vi_delete
    zvm_define_widget my_zvm_vi_change
    zvm_define_widget my_zvm_vi_change_eol
    zvm_define_widget my_zvm_vi_put_after
    zvm_define_widget my_zvm_vi_put_before
    zvm_define_widget my_zvm_vi_substitute
    zvm_define_widget my_zvm_vi_substitute_whole_line
    zvm_define_widget my_zvm_vi_replace_selection

    zvm_bindkey vicmd 'C' my_zvm_vi_change_eol
    zvm_bindkey vicmd 'P' my_zvm_vi_put_before
    zvm_bindkey vicmd 'S' my_zvm_vi_substitute_whole_line
    zvm_bindkey vicmd 'p' my_zvm_vi_put_after

    zvm_bindkey visual 'p' my_zvm_vi_replace_selection
    zvm_bindkey visual 'c' my_zvm_vi_change
    zvm_bindkey visual 'd' my_zvm_vi_delete
    zvm_bindkey visual 's' my_zvm_vi_substitute
    zvm_bindkey visual 'x' my_zvm_vi_delete
    zvm_bindkey visual 'y' my_zvm_vi_yank
}
